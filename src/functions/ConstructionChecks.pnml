
// Single Houses
switch(FEAT_HOUSES, SELF, ConstructionCheckSingleHouse, 
	!FuncServicePASS() 				                // Build where there is no Passenger service
        ||                                              // OR
    town_zone == TOWNZONE_EDGE    					// In EDGE
) {return;}	

switch(FEAT_HOUSES, SELF, switch_single_house_con_check, 
	!FuncServicePASS() 				                // Build where there is no Passenger service
        ||                                              // OR
    town_zone == TOWNZONE_EDGE    					// In EDGE
) {return;}	


// Shotengai
switch(FEAT_HOUSES, SELF, ConstructionCheckShotengai, 
	current_year < 1945
        ||
    (current_year >= 1945 && !FuncServicePASS() )
) {return;}	

// Small Height
switch (FEAT_HOUSES, SELF, switch_small_con_check, 	
	(FuncServicePASS() &&												// PASS Service AND
		(!PopOver7L8LThreshold() ||										// Not Above Threshold OR
			(PopOver7L8LThreshold() && 									// Above Threshold AND
				(!WideRoads() || 
					(WideRoads() && town_zone == TOWNZONE_OUTSKIRT) )	// Either not on Wide Roads OR on Wide Road in OUTSKIRT
			) 
		) 
	) 
	|| 
	(!FuncServicePASS() && 												// NO PASS Service AND
		(PopOver7L8LThreshold() && town_zone == TOWNZONE_CENTRE)		// Above threshold AND in CENTRE
	)
) {return;}	


// Medium Height
switch (FEAT_HOUSES, SELF, switch_medium_con_check, 	
	FuncServicePASS() &&					
		(
			(
				PopOver5L6LThreshold() && !PopOver7L8LThreshold() 
			) 
				||
			(
				PopOver7L8LThreshold() && 
				(
					!WideRoads() 
					|| 
					(WideRoads() &&  town_zone == TOWNZONE_OUTER_SUBURB)
				)
			)
		)	
	) {return;}	

// Large
switch (FEAT_HOUSES, SELF, switch_large_con_check, 	
	FuncServicePASS() &&		
		(			
			(PopOver7L8LThreshold() && !PopOver9L10LThreshold() && WideRoads() ) 
				||
			(PopOver9L10LThreshold() && 
				(
					(!WideRoads() && town_zone == TOWNZONE_CENTRE)
						|| 
					(WideRoads() && town_zone == TOWNZONE_INNER_SUBURB)
				)
			)
		)
	) {return;}	

// Xtra Large
switch (FEAT_HOUSES, SELF, switch_x_large_con_check, 	
	FuncServicePASS() &&						// Only build within the catchment area of a station which received passengers this month or last month
	PopOver9L10LThreshold() &&					// City has population above relevant threshold
	WideRoads()									// Can only build beside a wider road
	) {return;}	

// Skyscrapers
switch (FEAT_HOUSES, SELF, switch_skyscraper_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	IsUniqueHouse() &&						// Only one in city
	NoProhibitionShrine() &&				// No Prohibition shrine to block construction
	SkyscraperRoads() &&					// If parameter enabled, must built on wide roads
	PopOver11L12LThreshold() &&				// Population of city check
	(!TileGapByClass(3) ||					// There is another skyscraper within n tiles
	IfFirstSkyscraper() )					// OR it's the first skyscraper
) {return;}	

// Hospital
switch (FEAT_HOUSES, SELF, switch_hospital_con_check, 
	IsCity() && 							// Can only appear in Cities
	IsUniqueHouse()	&&						// Only one per city
	PopOver5L6LThreshold()					// Required city population
	) {return;}	

// Pachinko
switch (FEAT_HOUSES, SELF, switch_pachinko_con_check, 
	FuncServicePASS() &&		// Must have road to the SW
	TileGapByType(30)			// Can't appear within 30 tiles of another
) {return;}	

// Police Station
switch (FEAT_HOUSES, SELF, switch_police_station_con_check, 
	HasRoadAdjacentSW() &&					// Must have road to the SW
	PopOver5L6LThreshold() &&				// Population must be > 10k
	TileGapByType(30)						// Can't appear within 30 tiles of another
) {return;}	

// Shiro
switch (FEAT_HOUSES, SELF, switch_shiro_con_check, 
	IsCity() && 				// Can only appear in Cities
	IsUniqueHouse() && 			// Only one Shiro per city
	TownIndex() % 4 == 0 && 	// Proportion of cities
	OnGenerationOnly() 			// Will only spawn when Map is generated - not realistic being built in modern times!
	) {return;}	

// Temple
switch (FEAT_HOUSES, SELF, switch_temple_con_check, 
	IsCity() && 							// Can only appear in Cities
	IsUniqueHouse() && 						// Only one Temple per city
	OnGenerationOnly() && 					// Will only spawn when Map is generated - not realistic being built in modern times!
	nearby_tile_class(0,2) == TILE_CLASS_ROAD || nearby_tile_class(0,2) == TILE_CLASS_STATION 		// Requires a road to the SE, due to sticking out entrance
	) {return;}	

// Farm
switch (FEAT_HOUSES, SELF, switch_farm_con_check, 
	!IsCity() && 					// Cannot appear in Cities
	!PopOver5L6LThreshold()			// Population criteria
	) {return;}		
