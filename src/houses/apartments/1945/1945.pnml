// Begin apartments_1945

#include "src/houses/apartments/1945/1945_spritesets.pnml"
#include "src/houses/apartments/1945/1945_spritelayouts.pnml"

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_apartments_1945_all) {
	1: switch_haraku_6L_snow;
	1: switch_haraku_7L_snow;
	1: switch_haraku_8L_snow;
	1: switch_haraku_9L_snow;
	1: switch_kono_6L_snow;
	1: switch_kono_7L_snow;
	1: switch_kono_8L_snow;
	1: switch_kono_9L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1945_outer) {
	1: switch_haraku_6L_snow;
	1: switch_kono_6L_snow;
}

switch(FEAT_HOUSES, SELF, switch_apartments_1945_sprites, town_zone) {
	TOWNZONE_CENTRE: 		switch_apartments_1945_all;
	TOWNZONE_INNER_SUBURB:  switch_apartments_1945_all;
	switch_apartments_1945_outer;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_apartments_1945)
	{
		//1: PALETTE_CC_RED;			// too in your face
		2: PALETTE_CC_LIGHT_BLUE;
		//1: PALETTE_CC_PINK;			// too silly
		1: PALETTE_CC_DARK_BLUE;
		//1: PALETTE_CC_GREEN;			// nah
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_apartments_1945_con_check, 	
	FuncServicePASS() 				// Only build within the catchment area of a station which received passengers this month or last month
) {return;}	

item(FEAT_HOUSES, item_apartments_1945, 30, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_HARAKU);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1945,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB,TOWNZONE_OUTER_SUBURB), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;		
		}
	graphics {
		default: 					switch_apartments_1945_sprites;
		construction_check: 		switch_apartments_1945_con_check;
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_apartments_1945;
	}
}

// End apartments_1945
