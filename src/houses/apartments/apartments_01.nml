// Begin apartments_01

// Building Sprites
spriteset (spr_apartments_01, 							"src/gfx/apartments/apartments_01/building.png") 				{template_house_80(1,1)}
spriteset (spr_apartments_01_snow, 						"src/gfx/apartments/apartments_01/building.png") 				{template_house_80(1,82)}

// Ground Tiles
spriteset (spr_apartments_01_ground, 					"src/gfx/apartments/apartments_01/ground.png") 					{template_ground(1,1)}
spriteset (spr_apartments_01_ground_snow, 				"src/gfx/apartments/apartments_01/ground.png") 					{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_apartments_01_norm {
	ground {
		sprite: spr_apartments_01_ground (construction_state);
	}
	building {
		sprite: spr_apartments_01 (construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_apartments_01_snow {
	ground {
		sprite: spr_apartments_01_ground_snow (construction_state);
	}
	building {
		sprite: spr_apartments_01_snow(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_apartments_01_snow,terrain_type) {
	TILETYPE_SNOW: sprlay_apartments_01_snow;
	sprlay_apartments_01_norm;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_switch_recolor_apartments_01)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_apartments_01_con_check, 	
	FuncServicePASS() 				// Only build within the catchment area of a station which received passengers this month or last month
	) {return;}	

item(FEAT_HOUSES, item_apartments_01, 27, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_01);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1960,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB,TOWNZONE_OUTER_SUBURB), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;
				
		}
	graphics {
		default: 					switch_apartments_01_snow;
		construction_check: 		switch_apartments_01_con_check;
		cargo_production: 			func_produce(13,4);
		colour: 					rand_switch_recolor_apartments_01;
	}
}

// End apartments_01
