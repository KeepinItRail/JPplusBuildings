// Begin apartments_06

// Building Sprites
spriteset (spr_apartments_06, 							"src/gfx/apartments/apartments_06/building.png") 					{template_house_150_wide(1,1)}
spriteset (spr_apartments_06_snow, 						"src/gfx/apartments/apartments_06/building.png") 					{template_house_150_wide(1,152)}

// Ground Tiles
spriteset (spr_apartments_06_ground, 					"src/gfx/apartments/apartments_06/ground.png") 						{template_ground(1,1)}
spriteset (spr_apartments_06_ground_snow, 				"src/gfx/apartments/apartments_06/ground.png") 						{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_apartments_06_norm {
	ground {
		sprite: spr_apartments_06_ground (construction_state);
	}
	building {
		sprite: spr_apartments_06 (construction_state);
		yoffset:  2;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_apartments_06_snow {
	ground {
		sprite: spr_apartments_06_ground_snow (construction_state);
	}
	building {
		sprite: spr_apartments_06_snow (construction_state);
		yoffset:  2;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_apartments_06_snow,terrain_type) {
	TILETYPE_SNOW: sprlay_apartments_06_snow;
	sprlay_apartments_06_norm;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_apartments_06)
	{
		//1: PALETTE_CC_RED;			// too in your face
		2: PALETTE_CC_LIGHT_BLUE;
		//1: PALETTE_CC_PINK;			// too silly
		1: PALETTE_CC_DARK_BLUE;
		//1: PALETTE_CC_GREEN;			// nah
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_apartments_06_con_check, 	
	FuncServicePASS() 				// Only build within the catchment area of a station which received passengers this month or last month
) {return;}	

item(FEAT_HOUSES, item_apartments_06, 33, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_06);
		population: 				200;
		accepted_cargos: 			[[PASS, 8],[MAIL, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1960,1999];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(						// Taller than other Apartments so limited to TOWNZONE_CENTRE
										TOWNZONE_CENTRE				// 4
										//TOWNZONE_INNER_SUBURB,	// 3
										//TOWNZONE_OUTER_SUBURB,	// 2
										//TOWNZONE_OUTSKIRT			// 1
										//TOWNZONE_EDGE				// 0
									), bitmask(
										CLIMATE_TEMPERATE,
										CLIMATE_ARCTIC, ABOVE_SNOWLINE,
										CLIMATE_TROPIC
									)];
		building_class:				4;		
		}
	graphics {
		default: 					switch_apartments_06_snow;
		construction_check: 		switch_apartments_06_con_check;
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_apartments_06;
	}
}

// End apartments_06
