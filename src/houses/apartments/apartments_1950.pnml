// Begin Apartments_1950

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_apartments_1950_all) {
	1: switch_hayashi_4L_snow;
	1: switch_hirata_3L_snow;
	1: switch_hirata_4L_snow;
	1: switch_hirata_5L_snow;
	1: switch_hirata_6L_snow;
	1: switch_ikeda_4L_snow;
	1: switch_kimura_4L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1950_all_name) {
	dependent: switch_apartments_1950_all;
	1: string(NAME_APARTMENTS_HAYASHI);
	1: string(NAME_APARTMENTS_HIRATA);
	1: string(NAME_APARTMENTS_HIRATA);
	1: string(NAME_APARTMENTS_HIRATA);
	1: string(NAME_APARTMENTS_HIRATA);
	1: string(NAME_APARTMENTS_IKEDA);
	1: string(NAME_APARTMENTS_KIMURA);
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1950_outer) {
	1: switch_hayashi_3L_snow;
	1: switch_hirata_3L_snow;
	1: switch_hirata_4L_snow;
	1: switch_ikeda_3L_snow;
	1: switch_kimura_3L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1950_outer_name) {
	dependent: switch_apartments_1950_outer;
	1: string(NAME_APARTMENTS_HAYASHI);
	1: string(NAME_APARTMENTS_HIRATA);
	1: string(NAME_APARTMENTS_HIRATA);
	1: string(NAME_APARTMENTS_IKEDA);
	1: string(NAME_APARTMENTS_KIMURA);
}

switch(FEAT_HOUSES, SELF, switch_apartments_1950_sprites, town_zone) {
	TOWNZONE_CENTRE: 		switch_apartments_1950_all;
	TOWNZONE_INNER_SUBURB:  switch_apartments_1950_all;
	switch_apartments_1950_outer;
}

switch(FEAT_HOUSES, SELF, switch_apartments_1950_name, town_zone) {
	TOWNZONE_CENTRE: 		switch_apartments_1950_all_name;
	TOWNZONE_INNER_SUBURB:  switch_apartments_1950_all_name;
	switch_apartments_1950_outer_name;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_apartments_1950)
	{
		//1: PALETTE_CC_RED;			// too in your face
		2: PALETTE_CC_LIGHT_BLUE;
		//1: PALETTE_CC_PINK;			// too silly
		1: PALETTE_CC_DARK_BLUE;
		//1: PALETTE_CC_GREEN;			// nah
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_apartments_1950_con_check, 	
	FuncServicePASS() 				// Only build within the catchment area of a station which received passengers this month or last month
) {return;}	

item(FEAT_HOUSES, item_apartments_1950, 45, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_HARAKU);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1950,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB,TOWNZONE_OUTER_SUBURB), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;		
		}
	graphics {
		name:						switch_apartments_1950_name;
		default: 					switch_apartments_1950_sprites;
		construction_check: 		switch_apartments_1950_con_check;
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_apartments_1950;
	}
}

// End apartments_1950
