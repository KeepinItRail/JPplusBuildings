// Begin apartments_1960

#include "src/houses/apartments/tetsui/tetsui.pnml"
#include "src/houses/apartments/yano/yano.pnml"
#include "src/houses/apartments/yanagi/yanagi.pnml"

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_apartments_1960_all) {
	1: switch_yano_snow;
	1: switch_yanagi_5L_snow;
	1: switch_yanagi_6L_snow;
	1: switch_yanagi_7L_snow;
	1: switch_yanagi_8L_snow;
	1: switch_tetsui_5L_snow;
	1: switch_tetsui_6L_snow;
	1: switch_tetsui_7L_snow;
	1: switch_tetsui_8L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1960_all_name) {
	dependent: switch_apartments_1960_all;
	1: string(NAME_APARTMENTS_YANO);
	1: string(NAME_APARTMENTS_YANAGI);
	1: string(NAME_APARTMENTS_YANAGI);
	1: string(NAME_APARTMENTS_YANAGI);
	1: string(NAME_APARTMENTS_YANAGI);
	1: string(NAME_APARTMENTS_TETSUI);
	1: string(NAME_APARTMENTS_TETSUI);
	1: string(NAME_APARTMENTS_TETSUI);
	1: string(NAME_APARTMENTS_TETSUI);
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1960_outer) {
	1: switch_yano_snow;
	1: switch_yanagi_5L_snow;
	1: switch_yanagi_6L_snow;
	1: switch_tetsui_5L_snow;
	1: switch_tetsui_6L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_1960_outer_name) {
	dependent: switch_apartments_1960_outer;
	1: string(NAME_APARTMENTS_YANO);
	1: string(NAME_APARTMENTS_YANAGI);
	1: string(NAME_APARTMENTS_YANAGI);
	1: string(NAME_APARTMENTS_TETSUI);
	1: string(NAME_APARTMENTS_TETSUI);
}

switch(FEAT_HOUSES, SELF, switch_apartments_1960_sprites, town_zone) {
	TOWNZONE_CENTRE: 		switch_apartments_1960_all;
	TOWNZONE_INNER_SUBURB:  switch_apartments_1960_all;
	switch_apartments_1960_outer;
}

switch(FEAT_HOUSES, SELF, switch_apartments_1960_name, town_zone) {
	TOWNZONE_CENTRE: 		switch_apartments_1960_all_name;
	TOWNZONE_INNER_SUBURB:  switch_apartments_1960_all_name;
	switch_apartments_1960_outer_name;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_switch_recolor_apartments_1960)
	{
		//1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		//1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_apartments_1960_con_check, 	
	FuncServicePASS() 				// Only build within the catchment area of a station which received passengers this month or last month
	) {return;}	

item(FEAT_HOUSES, item_apartments_1960, 27, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_YANO);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1960,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB,TOWNZONE_OUTER_SUBURB), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;
				
		}
	graphics {
		name:						switch_apartments_1960_name;
		default: 					switch_apartments_1960_sprites;
		construction_check: 		switch_apartments_1960_con_check;
		cargo_production: 			func_produce(13,4);
		colour: 					rand_switch_recolor_apartments_1960;
	}
}
