// Begin Apartments_post_5L6L

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_apartments_post_5L6L_sprites) {
	1: switch_fukuda_6L_snow;
	1: switch_hayashi_6L_snow;
	1: switch_hayashi_5L_snow;
	1: switch_hirata_6L_snow;
	1: switch_hirata_5L_snow;
	1: switch_kimura_6L_snow;
	1: switch_kimura_5L_snow;
	1: switch_murakami_6L_snow;
	1: switch_murakami_5L_snow;
	//1: switch_shibata_5L_snow;
	//1: switch_tetsui_5L_snow;
	//1: switch_toki_5L_snow;
	//1: switch_yanagi_5L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_apartments_post_5L6L_name) {
	dependent: switch_apartments_post_5L6L_sprites;
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_FUKUDA),string(PERIOD_POSTWAR),string(L6));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_HAYASHI),string(PERIOD_POSTWAR),string(L6));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_HAYASHI),string(PERIOD_POSTWAR),string(L5));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_HIRATA),string(PERIOD_POSTWAR),string(L6));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_HIRATA),string(PERIOD_POSTWAR),string(L5));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_KIMURA),string(PERIOD_POSTWAR),string(L6));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_KIMURA),string(PERIOD_POSTWAR),string(L5));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_MURAKAMI),string(PERIOD_POSTWAR),string(L6));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_MURAKAMI),string(PERIOD_POSTWAR),string(L5));
	//1: string(NAME_APARTMENTS_SHIBATA);
	//1: string(NAME_APARTMENTS_TETSUI);
	//1: string(NAME_APARTMENTS_TOKI);
	//1: string(NAME_APARTMENTS_YANAGI);
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_apartments_post_5L6L)
	{
		//1: PALETTE_CC_RED;			// too in your face
		2: PALETTE_CC_LIGHT_BLUE;
		//1: PALETTE_CC_PINK;			// too silly
		1: PALETTE_CC_DARK_BLUE;
		//1: PALETTE_CC_GREEN;			// nah
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_apartments_post_5L6L_con_check, 	
	FuncServicePASS() &&				// Only build within the catchment area of a station which received passengers this month or last month
	PopOver5L6LResidentialThreshold()	// Only build if above population threshold
) {return;}	

item(FEAT_HOUSES, item_apartments_post_5L6L){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_HARADA);
		population: 				100;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1945,1959];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;		
		}
	graphics {
		name:						switch_apartments_post_5L6L_name;
		default: 					switch_apartments_post_5L6L_sprites;
		construction_check: 		switch_apartments_post_5L6L_con_check;
		cargo_production: 			func_produce(10,4);
		colour: 					rand_recolor_apartments_post_5L6L;
	}
}

// End apartments_post_5L6L
