// Begin cinema

// Building Sprites
spriteset (spr_cinema, 							"src/gfx/shops/cinema/building.png") 						{template_house_80(1,1)}
spriteset (spr_cinema_snow, 						"src/gfx/shops/cinema/building.png") 						{template_house_80(1,82)}

// Ground Tiles
spriteset (spr_cinema_ground, 					"src/gfx/shops/cinema/ground.png") 						{template_ground(1,1)}
spriteset (spr_cinema_ground_snow, 				"src/gfx/shops/cinema/ground.png") 						{template_ground(1,33)}

// Spritelayouts
spritelayout sprlay_cinema_norm {
	ground {
		sprite: spr_cinema_ground(construction_state);
	}
	building {
		sprite: spr_cinema(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_cinema_snow {
	ground {
		sprite: spr_cinema_ground(construction_state);
	}
	building {
		sprite: spr_cinema_snow(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_cinema_sprites,terrain_type) {
	TILETYPE_SNOW: sprlay_cinema_snow;
	sprlay_cinema_norm;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, random_recolour_cinema) {
	1:	return PALETTE_CC_DARK_BLUE;
	1:	return PALETTE_CC_GREY;
	1:	return PALETTE_CC_BROWN;
	1:	return PALETTE_CC_MAUVE;
	1:	return PALETTE_CC_WHITE;
	1:	return PALETTE_CC_DARK_GREEN;
}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_cinema_con_check, 
	FuncServicePASS() &&			// Only build within the catchment area of a station which received passengers this month or last month
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	Class2PopCheck() &&				// Is there population to allow another Class 2
	ClusterBuildingClass()			// Must be within two tiles of the same building class
) {return;}	

item(FEAT_HOUSES, item_cinema) {
	property {
		substitute: 				30;
		name: 						string(NAME_CINEMA);
		population: 				60;
		mail_multiplier: 			75;
		accepted_cargos: 			[[PASS, 8],[MAIL,2],[FOOD, 2]];
		local_authority_impact: 	180;
		removal_cost_multiplier: 	155;
		probability: 				5;
		years_available: 			[1950,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
	}
	graphics {
		default: 					switch_cinema_sprites;
		construction_check: 		switch_cinema_con_check;
		colour: 					random_recolour_cinema;
		cargo_production: 			func_produce(24,10);
	}
}

// End cinema
