// Begin boom_3L4L

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_boom_3L4L_sprites) {
	10: switch_nakamura_3L_snow;
	10: switch_nakamura_4L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_boom_3L4L_name) {
	dependent: switch_boom_3L4L_sprites;
	10: string(STR_CONCAT_3, string(NAME_APARTMENTS_NAKAMURA),string(PERIOD_BOOM),string(L3));
	10: string(STR_CONCAT_3, string(NAME_APARTMENTS_NAKAMURA),string(PERIOD_BOOM),string(L4));
}
// Construction Check
switch (FEAT_HOUSES, SELF, switch_boom_3L4L_con_check, 	
	(FuncServicePASS() &&															// PASS Service AND
		(!PopOver7L8LThreshold() ||										// Not Above Threshold OR
			(PopOver7L8LThreshold() && 									// Above Threshold AND
				(!WideRoads() || 
					(WideRoads() && (town_zone == TOWNZONE_EDGE || town_zone == TOWNZONE_OUTSKIRT)) )	// Either not on Wide Roads OR on Wide Road in EDGE
			) 
		) 
	) 
	|| 
	(!FuncServicePASS() && 															// NO PASS Service AND
		(PopOver7L8LThreshold() && town_zone == TOWNZONE_CENTRE)			// Above threshold AND in CENTRE
	)
) {return;}	

item(FEAT_HOUSES, item_boom_3L4L){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_HARADA);
		population: 				75;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				3;
		years_available: 			[1960,1979];
		minimum_lifetime: 			5;
		availability_mask: 			[ALL_TOWNZONES, ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;		
		}
	graphics {
		name:						switch_boom_3L4L_name;
		default: 					switch_boom_3L4L_sprites;
		construction_check: 		switch_boom_3L4L_con_check;
		cargo_production: 			func_produce(6,2);
	}
}