// Begin late_9L10L_21

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_late_9L10L_sprites) {
	1: switch_fukuda_9L_snow;
	1: switch_harada_9L_snow;
	1: switch_kono_9L_snow;

	1: switch_aoki_office_10L_snow;
	1: switch_hashimoto_office_tower_9L_snow;
	1: switch_obuilding_snow;
	2: switch_okada_office_tower_10L_snow;
	1: switch_takagi_10L_snow;
	1: switch_takemaru_shopping_centre_9L_snow;
	1: switch_tsuzumi_electronics_centre_10L_snow;
	1: switch_yamada_electronics_centre_10L_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_late_9L10L_name) {
	dependent: switch_late_9L10L_sprites;
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_FUKUDA),string(PERIOD_LATE),string(L9));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_HARADA),string(PERIOD_LATE),string(L9));
	1: string(STR_CONCAT_3, string(NAME_APARTMENTS_KONO),string(PERIOD_LATE),string(L9));

	1: string(STR_CONCAT_3, string(NAME_AOKI_OFFICE),string(PERIOD_LATE),string(L10));
	1: string(STR_CONCAT_3, string(NAME_HASHIMOTO_OFFICE_TOWER),string(PERIOD_LATE),string(L9));
	1: string(STR_CONCAT_3, string(NAME_OBUILDING),string(PERIOD_LATE),string(L10));
	2: string(STR_CONCAT_3, string(NAME_OKADA_OFFICE_TOWER),string(PERIOD_LATE),string(L10));
	1: string(STR_CONCAT_3, string(NAME_TAKAGI),string(PERIOD_LATE),string(L10));
	1: string(STR_CONCAT_3, string(NAME_TAKEMARU_SHOPPING_CENTRE),string(PERIOD_LATE),string(L9));
	1: string(STR_CONCAT_3, string(NAME_TSUZUMI_ELECTRONICS_CENTRE),string(PERIOD_LATE),string(L10));
	1: string(STR_CONCAT_3, string(NAME_YAMADA_ELECTRONICS_CENTRE),string(PERIOD_LATE),string(L10));
}

// Colours
random_switch (FEAT_HOUSES, SELF, switch_late_9L10L_colors) {
	dependent: switch_late_9L10L_sprites;	
	1: switch_fukuda_colors;
	1: switch_harada_colors;
	1: switch_kono_colors;

	1: switch_aoki_office_colors;
	1: switch_hashimoto_office_tower_colors;
	1: switch_obuilding_colors;
	2: switch_okada_office_tower_colors;
	1: switch_takagi_colors;
	1: switch_takemaru_shopping_centre_colors;
	1: switch_tsuzumi_electronics_centre_colors;
	1: switch_yamada_electronics_centre_colors;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_late_9L10L_con_check, 	
	FuncServicePASS() &&						// Only build within the catchment area of a station which received passengers this month or last month
	PopOver9L10LResidentialThreshold() &&		// City has population above relevant threshold
	WideRoads()									// Can only build beside a wider road
	) {return;}	

item(FEAT_HOUSES, item_late_9L10L){
	property {
		substitute: 				27;
		name: 						string(NAME_APARTMENTS_YANO);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1980,1999];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				4;
				
		}
	graphics {
		name:						switch_late_9L10L_name;
		default: 					switch_late_9L10L_sprites;
		construction_check: 		switch_late_9L10L_con_check;
		cargo_production: 			func_produce(18,6);
		colour: 					switch_late_9L10L_colors;
	}
}
