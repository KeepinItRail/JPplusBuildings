// Begin Commercial_1990

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_commercial_1990_sprites) {
	1: switch_obuilding_snow;
	1: switch_takada_snow;
	1: switch_ybuilding_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_commercial_1990_name) {
	dependent: switch_commercial_1990_sprites;
	1: string(NAME_OBUILDING);
	1: string(NAME_TAKADA);
	1: string(NAME_YBUILDING);
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_commercial_1990)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_commercial_1990_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	Class2PopCheck() &&				// Is there population to allow another Class 2?
	ClusterBuildingClass()			// Must be within two tiles of the same building class
) {return;}	

item(FEAT_HOUSES, item_commercial_1990, 90, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_XBUILDING);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 4],[GOOD, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				5;
		years_available: 			[1990,1999];
		minimum_lifetime: 			8;
		availability_mask: 			[ALL_TOWNZONES, bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
		}
	graphics {
		name:						switch_commercial_1990_name;
		default: 					switch_commercial_1990_sprites;
		construction_check: 		switch_commercial_1990_con_check;
		protection:					ProtectClassTwo(); 	 	
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_commercial_1990;
	}
}

// End X-Building
