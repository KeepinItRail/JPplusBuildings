// Begin boom_7L8L

// Sprites
random_switch (FEAT_HOUSES, SELF, switch_boom_7L8L_sprites) {
	1: switch_aoki_office_8L_snow;
	1: switch_aoyama_office_8L_snow;
	1: switch_aoyama_office_7L_snow;
	1: switch_okada_office_tower_8L_snow;
	1: switch_old_office_building_7L_snow;
	1: switch_old_office_building_8L_snow;
	1: switch_yamada_electronics_centre_8L_snow;
}

// Names
random_switch (FEAT_HOUSES, SELF, switch_boom_7L8L_name) {
	dependent: switch_boom_7L8L_sprites;
	1: string(STR_CONCAT_3, string(NAME_AOKI_OFFICE),string(PERIOD_BOOM),string(L8));
	1: string(STR_CONCAT_3, string(NAME_AOYAMA_OFFICE),string(PERIOD_BOOM),string(L8));
	1: string(STR_CONCAT_3, string(NAME_AOYAMA_OFFICE),string(PERIOD_BOOM),string(L7));
	1: string(STR_CONCAT_3, string(NAME_OKADA_OFFICE_TOWER),string(PERIOD_BOOM),string(L8));
	1: string(STR_CONCAT_3, string(NAME_OLD_OFFICE_BUILDING),string(PERIOD_BOOM),string(L7));
	1: string(STR_CONCAT_3, string(NAME_OLD_OFFICE_BUILDING),string(PERIOD_BOOM),string(L8));
	1: string(STR_CONCAT_3, string(NAME_YAMADA_ELECTRONICS_CENTRE),string(PERIOD_BOOM),string(L8));
}

// Colours
random_switch (FEAT_HOUSES, SELF, switch_boom_7L8L_colors)
	{
	dependent: switch_boom_7L8L_sprites;
	1: switch_aoki_office_colors;
	1: switch_aoyama_office_colors;
	1: switch_aoyama_office_colors;
	1: switch_okada_office_tower_colors;
	1: switch_old_office_building_colors;
	1: switch_old_office_building_colors;
	1: switch_yamada_electronics_centre_colors;
}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_boom_7L8L_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	CommercialCargoes() &&					// Commerical Cargo Requirement as per parameter
	CommercialPopCheck() &&					// Is there population to allow another Commercial?
	ClusterBuildingClass()	&&				// Must be within two tiles of the same building class
	//Population threshold to build this height of building
	PopOver7L8LCommercialThreshold()
) {return;}	

item(FEAT_HOUSES, item_commercial_boom_7L8L){
	property {
		substitute: 				27;
		name: 						string(NAME_OLD_OFFICE_BUILDING);
		population: 				120;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				3;
		years_available: 			[1960,1979];
		minimum_lifetime: 			8;
		availability_mask: 			[bitmask(
										TOWNZONE_CENTRE,
										TOWNZONE_INNER_SUBURB,
										TOWNZONE_OUTER_SUBURB), 
									bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			10;
		}
	graphics {
		default: 					switch_boom_7L8L_sprites;
		name:						switch_boom_7L8L_name;
		construction_check: 		switch_boom_7L8L_con_check;
		protection:					ProtectCommercial();		
		cargo_production: 			func_produce(13,4);
		colour: 					switch_boom_7L8L_colors;
	}
}

// End boom_7L8L
