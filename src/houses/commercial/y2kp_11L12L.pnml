// Begin y2kp_11L12L

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_y2kp_11L12L_sprites) {
	1: switch_aoki_office_12L_snow;
	1: switch_obuilding_snow;
	2: switch_okada_office_tower_12L_snow;
	1: switch_takagi_11L_snow;
	1: switch_ybuilding_snow;
}

random_switch (FEAT_HOUSES, SELF, switch_y2kp_11L12L_name) {
	dependent: switch_y2kp_11L12L_sprites;
	1: string(STR_CONCAT_3, string(NAME_AOKI_OFFICE),string(PERIOD_POSTY2K),string(L12));
	1: string(STR_CONCAT_3, string(NAME_OBUILDING),string(PERIOD_POSTY2K),string(L12));
	2: string(STR_CONCAT_3, string(NAME_OKADA_OFFICE_TOWER),string(PERIOD_POSTY2K),string(L12));
	1: string(STR_CONCAT_3, string(NAME_TAKAGI),string(PERIOD_POSTY2K),string(L11));
	1: string(STR_CONCAT_3, string(NAME_YBUILDING),string(PERIOD_POSTY2K),string(L12));
}

// Colours
random_switch (FEAT_HOUSES, SELF, switch_y2kp_11L12L_colors)
	{
	dependent: switch_y2kp_11L12L_sprites;
	1: switch_aoki_office_colors;
	1: switch_obuilding_colors;
	2: switch_okada_office_tower_colors;
	1: switch_takagi_colors;
	1: switch_ybuilding_colors;
}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_y2kp_11L12L_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	Class2PopCheck() &&				// Is there population to allow another Class 2?
	ClusterBuildingClass()			// Must be within two tiles of the same building class
) {return;}	

item(FEAT_HOUSES, item_y2kp_11L12L, 99, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_XBUILDING);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 4],[GOOD, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				5;
		years_available: 			[2000,0xFFFF];
		minimum_lifetime: 			8;
		availability_mask: 			[ALL_TOWNZONES, bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
		}
	graphics {
		name:						switch_y2kp_11L12L_name;
		default: 					switch_y2kp_11L12L_sprites;
		construction_check: 		switch_y2kp_11L12L_con_check;
		protection:					ProtectClassTwo(); 	 	
		cargo_production: 			func_produce(13,4);
		colour: 					switch_y2kp_11L12L_colors;
	}
}

// End X-Building
