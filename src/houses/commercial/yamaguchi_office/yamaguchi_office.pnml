// Begin yamaguchi_office

// Building Sprites
spriteset (spr_yamaguchi_office_norm, 					"src/houses/commercial/yamaguchi_office/building.png") 					{template_house_128(1,1)}
spriteset (spr_yamaguchi_office_snow, 					"src/houses/commercial/yamaguchi_office/building.png") 					{template_house_128(1,130)}

// Ground Tiles
spriteset (spr_yamaguchi_office_ground, 				"src/houses/commercial/yamaguchi_office/ground.png") 					{template_ground(1,1)}
spriteset (spr_yamaguchi_office_ground_snow, 			"src/houses/commercial/yamaguchi_office/ground.png") 					{template_ground(1,33)}

spritelayout sprlay_yamaguchi_office_norm {
	ground {
		sprite: spr_yamaguchi_office_ground(3);
	}
	building {
		sprite: spr_yamaguchi_office_norm(3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_yamaguchi_office_snow {
	ground {
		sprite: spr_yamaguchi_office_ground_snow(3);
	}
	building {
		sprite: spr_yamaguchi_office_snow(3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_yamaguchi_office_snow,terrain_type) {
	TILETYPE_SNOW: sprlay_yamaguchi_office_snow;
	sprlay_yamaguchi_office_norm;
}


// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_yamaguchi_office)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		// 1: PALETTE_CC_PINK;		// Nah
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_yamaguchi_office_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	Class2PopCheck() &&				// Is there population to allow another Class 2?
	IsUniqueHouse()	&&				// Only one per city
	ClusterBuildingClass()			// Must be within two tiles of the same building class
) {return;}	

item(FEAT_HOUSES, item_yamaguchi_office, 36, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_YAMAGUCHI_OFFICE);
		population: 				180;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				5;
		years_available: 			[1960,0xFFFF];
		minimum_lifetime: 			8;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
		}
	graphics {
		default: 					switch_yamaguchi_office_snow;
		construction_check: 		switch_yamaguchi_office_con_check; 	
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_yamaguchi_office;
	}
}

// End yamaguchi_office
