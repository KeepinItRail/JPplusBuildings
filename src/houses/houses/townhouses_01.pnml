// Begin townhouses

// Building Sprites
spriteset (spr_townhouses_01_a, 					"src/gfx/houses/townhouses_01/building_a.png") 			{template_house_80(1,1)}
spriteset (spr_townhouses_01_a_snow, 				"src/gfx/houses/townhouses_01/building_a.png") 			{template_house_80(1,82)}

spriteset (spr_townhouses_01_b, 					"src/gfx/houses/townhouses_01/building_b.png") 			{template_house_80(1,1)}
spriteset (spr_townhouses_01_b_snow, 				"src/gfx/houses/townhouses_01/building_b.png") 			{template_house_80(1,82)}

// Spritelayouts
spritelayout sprlay_townhouses_01_a_norm {
	ground {
		sprite: spr_ground_grass (construction_state);
	}
	building {
		sprite: spr_townhouses_01_a (construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_townhouses_01_a_snow {
	ground {
		sprite: spr_ground_snow (construction_state);
	}
	building {
		sprite: spr_townhouses_01_a_snow (construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_townhouses_01_b_norm {
	ground {
		sprite: spr_ground_grass (construction_state);
	}
	building {
		sprite: spr_townhouses_01_b (construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_townhouses_01_b_snow {
	ground {
		sprite: spr_ground_snow (construction_state);
	}
	building {
		sprite: spr_townhouses_01_b_snow (construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_townhouses_01_a_sprites, terrain_type) {
	TILETYPE_SNOW: sprlay_townhouses_01_a_snow;
	sprlay_townhouses_01_a_norm;
}

switch(FEAT_HOUSES, SELF, switch_townhouses_01_b_sprites, terrain_type) {
	TILETYPE_SNOW: sprlay_townhouses_01_b_snow;
	sprlay_townhouses_01_b_norm;
}

// Random selection of sprites
random_switch (FEAT_HOUSES, SELF, switch_townhouses_01_sprites) {
	1: switch_townhouses_01_a_sprites;		
	1: switch_townhouses_01_b_sprites;		
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_townhouses_01_a)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Checks

switch (FEAT_HOUSES, SELF, switch_townhouses_01_old_con_check, 
	HouseOfficeGap()
) {return;}	

switch (FEAT_HOUSES, SELF, switch_townhouses_01_new_con_check, 
	HouseOfficeGap() && 			// Can't build within two tiles of Building Class 2 (Offices, Shops and Skyscrapers)
	!FuncServicePASS() 				// Builds where there is no Passenger service
) {return;}	

item(FEAT_HOUSES, item_townhouses_old, 05, HOUSE_SIZE_1X1){
	property {
		substitute: 				06;
		name: 						string(NAME_TOWNHOUSES_01);
		population: 				30;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1870,1949];
		minimum_lifetime: 			2;
		availability_mask: 			[bitmask(								// Agreed to JPS
										TOWNZONE_CENTRE,			// 4
										TOWNZONE_INNER_SUBURB,		// 3
										TOWNZONE_OUTER_SUBURB,		// 2
										TOWNZONE_OUTSKIRT,			// 1
										TOWNZONE_EDGE				// 0
									), bitmask(
										CLIMATE_TEMPERATE,
										CLIMATE_ARCTIC, ABOVE_SNOWLINE,
										CLIMATE_TROPIC
									)];
		building_class:				1;
		
		}
	graphics {
		default: 					switch_townhouses_01_sprites;
		construction_check: 		switch_townhouses_01_old_con_check;
		destruction:				DestructionCheckHouses;
		cargo_production: 			func_produce(2,1);
		colour: 					rand_recolor_townhouses_01_a;
	}
}

item(FEAT_HOUSES, item_townhouses_new, 06, HOUSE_SIZE_1X1){
	property {
		substitute: 				06;
		name: 						string(NAME_TOWNHOUSES_01);
		population: 				30;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1945,0xFFFF];
		minimum_lifetime: 			2;
		availability_mask: 			[bitmask(								// Agreed to JPS
										TOWNZONE_CENTRE,			// 4
										TOWNZONE_INNER_SUBURB,		// 3
										TOWNZONE_OUTER_SUBURB,		// 2
										TOWNZONE_OUTSKIRT			// 1
										//TOWNZONE_EDGE				// 0
									), bitmask(
										CLIMATE_TEMPERATE,
										CLIMATE_ARCTIC, ABOVE_SNOWLINE,
										CLIMATE_TROPIC
									)];
		building_class:				1;
		
		}
	graphics {
		default: 					switch_townhouses_01_sprites;
		construction_check: 		switch_townhouses_01_new_con_check;
		cargo_production: 			func_produce(4,1);
		colour: 					rand_recolor_townhouses_01_a;
	}
}

// End townhouses
