// Begin shrine_01

// Ground Sprites
spriteset (spr_shrine_01_ground, 		"src/houses/landmarks/shrine_01/ground.png") 			{template_ground(1,1)}
spriteset (spr_shrine_01_ground_snow, 	"src/houses/landmarks/shrine_01/ground.png") 			{template_ground(1,33)}

// Buildings
spriteset (spr_shrine_01, 				"src/houses/landmarks/shrine_01/building.png") 			{template_house_80(1,1)}
spriteset (spr_shrine_01_snow, 			"src/houses/landmarks/shrine_01/building.png") 			{template_house_80(1,82)}

// Spritelayouts
spritelayout sprlay_shrine_01 {
	ground {
		sprite: spr_shrine_01_ground (3);
	}
	building {
		sprite: spr_shrine_01 (3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_shrine_01_snow {
	ground {
		sprite: spr_shrine_01_ground_snow (3);
	}
	building {
		sprite: spr_shrine_01_snow (3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_shrine_01_sprites,terrain_type) {
	TILETYPE_SNOW: sprlay_shrine_01_snow;
	sprlay_shrine_01;
}

// Random recolour
random_switch (FEAT_HOUSES, SELF, rand_recolor_shrine_01)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_shrine_01_con_check, 
	IsCity() &&							// Can only build in a city
	IsUniqueHouse() &&					// Only one per town
	OnGenerationOnly()					// Doesn't build during gamereplay, only upon map generation
) {return;}	

if (param_prohibition_shrine == 1) {	// Only appears if parameter is turned on (default: off)

	item(FEAT_HOUSES, item_shrine_01, 113, HOUSE_SIZE_1X1){
		property {
			substitute: 				06;
			name: 						string(NAME_SHRINE_01);
			building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED, HOUSE_FLAG_PROTECTED);
			population: 				40;									// Agreed to JPS
			accepted_cargos: 			[[PASS, 4]];
			local_authority_impact: 	0;
			removal_cost_multiplier: 	0;
			probability: 				15;									// High to make sure it spawns
			years_available: 			[0,0xFFFF];							// Agreed to JPS
			minimum_lifetime: 			2;										
			availability_mask: 			[bitmask(							// Agreed to JPS, also, all zones to give more opportunity to appear
											TOWNZONE_CENTRE,			// 4
											TOWNZONE_INNER_SUBURB,		// 3
											TOWNZONE_OUTER_SUBURB,		// 2
											TOWNZONE_OUTSKIRT,			// 1
											TOWNZONE_EDGE				// 0
										), bitmask(
											CLIMATE_TEMPERATE,
											CLIMATE_ARCTIC, ABOVE_SNOWLINE,
											CLIMATE_TROPIC
										)];
			//building_class:				0;
		
			}
		graphics {
			default: 					switch_shrine_01_sprites;
			construction_check: 		switch_shrine_01_con_check;
			cargo_production: 			func_produce(1,1);			
			colour: 					rand_recolor_shrine_01;
		}
	}
}

// End shrine_01
