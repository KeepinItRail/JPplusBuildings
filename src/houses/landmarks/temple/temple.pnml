// Begin temple

random_switch (FEAT_HOUSES, SELF, switch_temple_north_sprites) {
	1: switch_temple_01_north;
	1: switch_temple_02_north;
}

random_switch (FEAT_HOUSES, SELF, switch_temple_east_sprites) {
	dependent: switch_temple_north_sprites;
	1: switch_temple_01_east;
	1: switch_temple_02_east;
}

random_switch (FEAT_HOUSES, SELF, switch_temple_west_sprites) {
	dependent: switch_temple_north_sprites;
	1: switch_temple_01_west;
	1: switch_temple_02_west;
}

random_switch (FEAT_HOUSES, SELF, switch_temple_south_sprites) {
	dependent: switch_temple_north_sprites;
	1: switch_temple_01_south;
	1: switch_temple_02_south;
}

random_switch (FEAT_HOUSES, SELF, switch_temple_name) {
	dependent: switch_temple_north_sprites;
	1: string(NAME_TEMPLE_01);
	1: string(NAME_TEMPLE_02);
}


// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_temple)
	{
		//1: PALETTE_CC_RED;			Nah
		2: PALETTE_CC_LIGHT_BLUE;
		//1: PALETTE_CC_PINK;			Nah
		1: PALETTE_CC_DARK_BLUE;
		//1: PALETTE_CC_GREEN;			Nah
		2: PALETTE_CC_DARK_GREEN;
		//3: PALETTE_CC_CREAM;
		//1: PALETTE_CC_MAUVE;			Nah
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_temple_con_check, 
	IsCity() && 				// Can only appear in Cities
	IsUniqueHouse() && 			// Only one Temple per city
	OnGenerationOnly() && 		// Will only spawn when Map is generated - not realistic being built in modern times!
	HasRoadAdjacentSE2x2() &&	// Requires a road to the SE, due to sticking out entrance
	RandomBitsFour()) {return;}	// Think this makes the probability abouty a quarter

item(FEAT_HOUSES, item_temple, 66, HOUSE_SIZE_2X2){
	property {
		substitute: 				20; //Stadium since 2x2
		name: 						string(NAME_TEMPLE_01);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED, HOUSE_FLAG_CHURCH, HOUSE_FLAG_PROTECTED);
		population: 				100;
		accepted_cargos: 			[[PASS, 8],[MAIL, 1],[FOOD, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				5;
		years_available: 			[1700,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[ALL_TOWNZONES, bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				6;	
		}
	graphics {
		name:						switch_temple_name;				
		graphics_north: 			switch_temple_north_sprites;
		graphics_east: 				switch_temple_east_sprites;
		graphics_west: 				switch_temple_west_sprites;
		graphics_south: 			switch_temple_south_sprites;
		construction_check:			switch_temple_con_check;	
		cargo_production: 			func_produce(10,2);
		colour: 					rand_recolor_temple;
	}
}

// End temple
