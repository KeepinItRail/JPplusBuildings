// Begin aoki_office

// Building Sprites
spriteset (spr_aoki_office, 							"src/gfx/offices/aoki_office/building.png") 						{template_house_100(1,1)}
spriteset (spr_aoki_office_snow, 						"src/gfx/offices/aoki_office/building.png") 						{template_house_100(1,102)}

// Ground Tiles
spriteset (spr_aoki_office_ground, 					"src/gfx/offices/aoki_office/ground.png") 						{template_house_100(1,1)}
spriteset (spr_aoki_office_ground_snow, 				"src/gfx/offices/aoki_office/ground.png") 						{template_house_100(1,102)}

spritelayout sprlay_aoki_office_norm {
	ground {
		sprite: spr_aoki_office_ground(construction_state);
	}
	building {
		sprite: spr_aoki_office(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_aoki_office_snow {
	ground {
		sprite: spr_aoki_office_ground_snow(construction_state);
	}
	building {
		sprite: spr_aoki_office_snow(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_aoki_office_sprites,terrain_type) {
	TILETYPE_SNOW: sprlay_aoki_office_snow;
	sprlay_aoki_office_norm;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_aoki_office)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_aoki_office_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	Class2PopCheck() &&				// Is there population to allow another Class 2?
	ClusterBuildingClass()			// Must be within two tiles of the same building class
) {return;}	

item(FEAT_HOUSES, item_aoki_office, 49, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_AOKI_OFFICE);
		population: 				30;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				5;
		years_available: 			[1970,0xFFFF];
		minimum_lifetime: 			8;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE,TOWNZONE_INNER_SUBURB), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
		}
	graphics {
		default: 					switch_aoki_office_sprites;
		construction_check: 		switch_aoki_office_con_check; 	
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_aoki_office;
	}
}

// End aoki_office
