// Begin hotel_old

// Building Sprites
spriteset (spr_build_hotel_old_north, 							"src/gfx/offices/hotel_old/building.png") 					{template_house_128(1,1)}
spriteset (spr_build_hotel_old_east, 							"src/gfx/offices/hotel_old/building.png") 					{template_house_128(261,1)}

spriteset (spr_build_hotel_old_north_snow, 						"src/gfx/offices/hotel_old/building.png") 					{template_house_128(1,130)}
spriteset (spr_build_hotel_old_east_snow, 						"src/gfx/offices/hotel_old/building.png") 					{template_house_128(261,130)}

// Ground Sprites
spriteset (spr_ground_hotel_old_north, 							"src/gfx/offices/hotel_old/ground.png") 					{template_ground(1,1)}
spriteset (spr_ground_hotel_old_east, 							"src/gfx/offices/hotel_old/ground.png") 					{template_ground(261,1)}

spriteset (spr_ground_hotel_old_north_snow, 					"src/gfx/offices/hotel_old/ground.png") 					{template_ground(1,33)}
spriteset (spr_ground_hotel_old_east_snow, 						"src/gfx/offices/hotel_old/ground.png") 					{template_ground(261,33)}

// Normal Sprites
spritelayout sprlay_hotel_old_north {
	ground {
		sprite: spr_ground_hotel_old_north (construction_state);
	}
	building {
		sprite: spr_build_hotel_old_north (construction_state);
		xoffset:  5;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_hotel_old_east {
	ground {
		sprite: spr_ground_hotel_old_east (construction_state);
	}
	building {
		sprite: spr_build_hotel_old_east (construction_state);
		xoffset:  2;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_hotel_old_snow_north {
	ground {
		sprite: spr_ground_hotel_old_north_snow (construction_state);
	}
	building {
		sprite: spr_build_hotel_old_north_snow (construction_state);
		xoffset:  5;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_hotel_old_snow_east {
	ground {
		sprite: spr_ground_hotel_old_east_snow (construction_state);
	}
	building {
		sprite: spr_build_hotel_old_east_snow (construction_state);
		xoffset:  2;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_hotel_old_north,terrain_type) {
	TILETYPE_SNOW: sprlay_hotel_old_snow_north;
	sprlay_hotel_old_north;
}
switch(FEAT_HOUSES, SELF, switch_hotel_old_east,terrain_type) {
	TILETYPE_SNOW: sprlay_hotel_old_snow_east;
	sprlay_hotel_old_east;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_hotel_old)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_hotel_old_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	Class2PopCheck() &&				// Residential population is sufficient for another Skyscraper
	ClusterBuildingClass() &&		// Must be within two tiles of the same building class
	PopOver10k()					// Population requirement !!! TBU !!!
) {return;}		
 

item(FEAT_HOUSES, item_hotel_old, 115, HOUSE_SIZE_1X2){
	property {
		substitute: 				7; 													
		name: 						string(NAME_HOTEL_OLD);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED);
		population: 				200;
		accepted_cargos: 			[[PASS, 8],[MAIL, 4],[FOOD, 8],[BEER, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1955,1989];
		minimum_lifetime: 			20;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				2;	
		}
	graphics {
		graphics_north: 			switch_hotel_old_north;
		graphics_east: 				switch_hotel_old_east;
		construction_check:			switch_hotel_old_con_check;
		cargo_production: 			func_produce(24,10);
		colour: 					rand_recolor_hotel_old;
	}
}

// End hotel_old
