// Begin offices_01

// Building Sprites
spriteset (spr_offices_01, 							"src/gfx/offices/offices_01/building.png") 						{template_house_128_1dir(1,1)}
spriteset (spr_offices_01_snow, 						"src/gfx/offices/offices_01/building.png") 						{template_house_128_1dir(1,130)}

// Ground Tiles
spriteset (spr_offices_01_ground, 					"src/gfx/offices/offices_01/ground.png") 						{template_house_128_1dir(1,1)}
spriteset (spr_offices_01_ground_snow, 				"src/gfx/offices/offices_01/ground.png") 						{template_house_128_1dir(1,130)}

spritelayout sprlay_offices_01_norm {
	ground {
		sprite: spr_offices_01_ground(LOAD_TEMP(1));
	}
	building {
		sprite: spr_offices_01(LOAD_TEMP(1));
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_offices_01_snow {
	ground {
		sprite: spr_offices_01_ground_snow(LOAD_TEMP(1));
	}
	building {
		sprite: spr_offices_01_snow(LOAD_TEMP(1));
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose the sprites
switch (FEAT_HOUSES, SELF, switch_offices_01_sprites, [ 
 	// Check for snow and set the groundsprite in temporary variable 0
 	STORE_TEMP(GROUNDSPRITE_NORMAL, 0),
 	STORE_TEMP(terrain_type == TILETYPE_SNOW ? GROUNDSPRITE_SNOW : LOAD_TEMP(0), 0),
	// Check the rotation of the house
	STORE_TEMP(switch_road_adjacency(), 1),
	terrain_type == TILETYPE_SNOW])
	{1: sprlay_offices_01_snow; sprlay_offices_01_norm;}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_offices_01)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_offices_01_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	ModernOfficePopCheck() &&		// Residential population is sufficient for another modern office building class
	ClusterBuildingClass() &&		// Must be within two tiles of the same building class
	IsUniqueHouse()					// Only one per city
) {return;}	

item(FEAT_HOUSES, item_offices_01, 36, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						TTD_STR_TOWN_BUILDING_NAME_HOUSES_1;
		population: 				30;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1945,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
		}
	graphics {
		default: 					switch_offices_01_sprites;
		construction_check: 		switch_offices_01_con_check; 	
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_offices_01;
	}
}

// End offices_01
