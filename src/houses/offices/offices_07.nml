// Begin offices_07

// Building Sprites
spriteset (spr_offices_07, 							"src/gfx/offices/offices_07/building.png") 						{template_house_128(1,1)}
spriteset (spr_offices_07_snow, 					"src/gfx/offices/offices_07/building.png") 						{template_house_128(1,130)}

// Ground Tiles
spriteset (spr_offices_07_ground, 					"src/gfx/offices/offices_07/ground.png") 						{template_ground(1,1)}
spriteset (spr_offices_07_ground_snow, 				"src/gfx/offices/offices_07/ground.png") 						{template_ground(1,33)}

spritelayout sprlay_offices_07_norm {
	ground {
		sprite: spr_offices_07_ground(3);
	}
	building {
		sprite: spr_offices_07(3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_offices_07_snow {
	ground {
		sprite: spr_offices_07_ground_snow(3);
	}
	building {
		sprite: spr_offices_07_snow(3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_offices_07_sprites,terrain_type) {
	TILETYPE_SNOW: sprlay_offices_07_snow;
	sprlay_offices_07_norm;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_offices_07)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_offices_07_con_check, 
	FuncServicePASS() &&			// Must have passenger service
	CommercialCargoes() &&			// Commerical Cargo Requirement as per parameter
	ModernOfficePopCheck() &&		// Residential population is sufficient for another modern office building class
	ClusterBuildingClass()			// Must be within two tiles of the same building class
) {return;}	

item(FEAT_HOUSES, item_offices_07, 39, HOUSE_SIZE_1X1){
	property {
		substitute: 				27;
		name: 						string(NAME_OFFICES_07);
		population: 				150;
		accepted_cargos: 			[[PASS, 4],[MAIL, 4],[GOOD, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				5;
		years_available: 			[1970,1989];
		minimum_lifetime: 			8;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class: 			2;
		}
	graphics {
		default: 					switch_offices_07_sprites;
		construction_check: 		switch_offices_07_con_check; 	
		cargo_production: 			func_produce(13,4);
		colour: 					rand_recolor_offices_07;
	}
}

// End offices_07
