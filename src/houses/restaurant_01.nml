// Begin restaurant_01

// Building Sprites
spriteset (spr_build_restaurant_01, 							"src/gfx/shops/restaurant_01/building.png") 					{template_house_80(1,1)}
spriteset (spr_build_restaurant_01_snow, 						"src/gfx/shops/restaurant_01/building.png") 					{template_house_80(1,82)}

// Ground Sprites
spriteset (spr_ground_restaurant_01, 							"src/gfx/shops/restaurant_01/ground.png") 						{template_house_80(1,1)}
spriteset (spr_ground_restaurant_01_snow, 						"src/gfx/shops/restaurant_01/ground.png") 						{template_house_80(1,82)}

// Normal Sprites
spritelayout sprlay_restaurant_01 {
	ground {
		sprite: spr_ground_restaurant_01(construction_state);
	}
	building {
		sprite: spr_build_restaurant_01(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_restaurant_01_snow {
	ground {
		sprite: spr_ground_restaurant_01_snow(construction_state);
	}
	building {
		sprite: spr_build_restaurant_01_snow(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_restaurant_01,terrain_type) {
	TILETYPE_SNOW: sprlay_restaurant_01_snow;
	sprlay_restaurant_01;
}

// Recolouring

random_switch (FEAT_HOUSES, SELF, rand_recolor_restaurant_01)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}


// Construction Check
switch (FEAT_HOUSES, SELF, switch_restaurant_01_con_check, 
	HasRoadAdjacentSW() &&		// Must have road to the SW
	ThirtyTileGap() &&			// Can't appear within 30 tiles of another
	BanFromHistDistrict()		// Cannot build close to Temple
	) {return;}	

// Protection Check
switch (FEAT_HOUSES, SELF, switch_restaurant_01_pro_check, 
	FuncServicePASS() &&		// Must have PASS delivered
	FuncServiceFOOD()			// Must have PETR delivered
	) {return;}	

item(FEAT_HOUSES, item_restaurant_01, 64, HOUSE_SIZE_1X1){
	property {
		substitute: 				29; 													
		name: 						string(NAME_RESTAURANT_01);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED);	// Want flat land
		population: 				50;														// Agreed to JPS
		accepted_cargos: 			[[PASS, 4],[MAIL, 2],[FOOD,8]];							// Agreed to JPS and bumped up FOOD
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1950,0xFFFF];	
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(
										TOWNZONE_INNER_SUBURB,
										TOWNZONE_OUTER_SUBURB,
										TOWNZONE_OUTSKIRT
										), 
									bitmask(
										CLIMATE_TEMPERATE, 
										CLIMATE_ARCTIC, 
										ABOVE_SNOWLINE
										)];
		//building_class:			2;	
		}
	graphics {
		default: 					switch_restaurant_01;
		construction_check:			switch_restaurant_01_con_check;
		protection:					switch_restaurant_01_pro_check;
		cargo_production: 			func_produce(20,5);
		colour: 					rand_recolor_restaurant_01;
	}
}

// End restaurant_01
