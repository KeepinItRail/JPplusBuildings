// Begin petrol_station_02

// Building Sprites
spriteset (spr_build_petrol_station_02, 				"src/gfx/shops/petrol_station_02/building.png") 		{template_house_80(1,1)}
spriteset (spr_build_petrol_station_02_snow, 			"src/gfx/shops/petrol_station_02/building.png") 		{template_house_80(1,82)}

// Ground Sprites
spriteset (spr_ground_petrol_station_02, 				"src/gfx/shops/petrol_station_02/ground.png") 			{template_ground(1,1)}
spriteset (spr_ground_petrol_station_02_snow, 			"src/gfx/shops/petrol_station_02/ground.png") 			{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_petrol_station_02 {
	ground {
		sprite: spr_ground_petrol_station_02 (3);
	}
	building {
		sprite: spr_build_petrol_station_02 (3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_petrol_station_02_snow {
	ground {
		sprite: spr_ground_petrol_station_02_snow (3);
	}
	building {
		sprite: spr_build_petrol_station_02_snow (3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_petrol_station_02,terrain_type) {
	TILETYPE_SNOW: sprlay_petrol_station_02_snow;
	sprlay_petrol_station_02;
}

// Recolouring

random_switch (FEAT_HOUSES, SELF, rand_recolor_petrol_station_02)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}


// Construction Check
switch (FEAT_HOUSES, SELF, switch_petrol_station_02_con_check, 
	HasRoadAdjacentSW() &&		// Must have road to the SW
	HasRoadAdjacentSE() &&		// Must have road to the SE
	TileGap(30)					// Can't appear within 30 tiles of another
) {return;}	

// Protection Check
switch (FEAT_HOUSES, SELF, switch_petrol_station_02_pro_check, 
	HasRoadAdjacentSW() &&		// Must have road to the SW
	HasRoadAdjacentSE() &&		// Must have road to the SE
	FuncServicePETR()			// Must have PETR delivered
	) {return;}	

item(FEAT_HOUSES, item_petrol_station_02){
	property {
		substitute: 				29; 													
		name: 						string(NAME_PETROL_STATION_02);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED);		// Want flat land
		population: 				40;									// Agreed to JPS
		accepted_cargos: 			[
										[PASS, 4],[MAIL, 2],			// Of course
										[GOOD, 2],[PETR, 8]				// Goods & Petrol
									];							
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1950,0xFFFF];	
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(
										//TOWNZONE_CENTRE,
										//TOWNZONE_INNER_SUBURB,
										TOWNZONE_OUTER_SUBURB,
										TOWNZONE_OUTSKIRT,
										TOWNZONE_EDGE
										), 
									bitmask(
										CLIMATE_TEMPERATE, 
										CLIMATE_ARCTIC, 
										ABOVE_SNOWLINE
										)];
		//building_class:			2;	
		}
	graphics {
		default: 					switch_petrol_station_02;
		construction_check:			switch_petrol_station_02_con_check;
		protection:					switch_petrol_station_02_pro_check;
		cargo_production: 			func_produce(5,3);
		colour: 					rand_recolor_petrol_station_02;
	}
}

// End petrol_station_02
