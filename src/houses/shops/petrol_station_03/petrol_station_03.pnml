// Begin petrol_station_03

// Building Sprites
spriteset (spr_build_petrol_station_03, 			"src/houses/shops/petrol_station_03/building.png") 			{template_house_80(1,1)}
spriteset (spr_build_petrol_station_03_snow, 		"src/houses/shops/petrol_station_03/building.png") 			{template_house_80(1,82)}

// Ground Sprites
spriteset (spr_ground_petrol_station_03, 			"src/houses/shops/petrol_station_03/ground.png") 				{template_ground(1,1)}
spriteset (spr_ground_petrol_station_03_snow, 		"src/houses/shops/petrol_station_03/ground.png") 				{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_petrol_station_03_north {
	ground {
		sprite: spr_ground_petrol_station_03(0);
	}
	building {
		sprite: spr_build_petrol_station_03(0);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_petrol_station_03_west {
	ground {
		sprite: spr_ground_petrol_station_03(1);
	}
	building {
		sprite: spr_build_petrol_station_03(1);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_petrol_station_03_snow_north {
	ground {
		sprite: spr_ground_petrol_station_03_snow(0);
	}
	building {
		sprite: spr_build_petrol_station_03_snow(0);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_petrol_station_03_snow_west {
	ground {
		sprite: spr_ground_petrol_station_03_snow(1);
	}
	building {
		sprite: spr_build_petrol_station_03_snow(1);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_petrol_station_03_north,terrain_type) {
	TILETYPE_SNOW: sprlay_petrol_station_03_snow_north;
	sprlay_petrol_station_03_north;
}
switch(FEAT_HOUSES, SELF, switch_petrol_station_03_west,terrain_type) {
	TILETYPE_SNOW: sprlay_petrol_station_03_snow_west;
	sprlay_petrol_station_03_west;
}

// Recolouring

random_switch (FEAT_HOUSES, SELF, rand_recolor_petrol_station_03)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}


// Construction Check
switch (FEAT_HOUSES, SELF, switch_petrol_station_03_con_check, 
	HasRoadAdjacentSE() &&		// Must have road to the SW
	TileGapByType(30)			// Can't appear within 30 tiles of another
) {return;}	

// Protection Check
switch (FEAT_HOUSES, SELF, switch_petrol_station_03_pro_check, 
	HasRoadAdjacentSE() &&		// Must have road to the SW
	FuncServicePETR()			// Must have PETR delivered
	) {return;}	

item(FEAT_HOUSES, item_petrol_station_03, 110, HOUSE_SIZE_2X1){
	property {
		substitute: 				76; 													
		name: 						string(NAME_PETROL_STATION_01);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED,HOUSE_FLAG_PROTECTED);	// Want flat land
		population: 				80;														// Agreed to JPS
		accepted_cargos: 			[[PASS, 4],[MAIL, 2],[GOOD, 2],[PETR, 8]];				// Agreed to JPS and added Petrol
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1950,1989];	
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(
										//TOWNZONE_CENTRE,
										//TOWNZONE_INNER_SUBURB,
										TOWNZONE_OUTER_SUBURB,
										TOWNZONE_OUTSKIRT,
										TOWNZONE_EDGE
										), 
									bitmask(
										CLIMATE_TEMPERATE, 
										CLIMATE_ARCTIC, 
										ABOVE_SNOWLINE
										)];
		//building_class:			2;	
		}
	graphics {
		graphics_north: 			switch_petrol_station_03_north;
		graphics_west: 				switch_petrol_station_03_west;
		construction_check:			switch_petrol_station_03_con_check;
		protection:					switch_petrol_station_03_pro_check;
		cargo_production: 			func_produce(20,5);
		colour: 					rand_recolor_petrol_station_03;
	}
}

// End petrol_station_03
