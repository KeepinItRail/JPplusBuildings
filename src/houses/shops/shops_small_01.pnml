// Begin shops_small_01

// Building Sprites
spriteset (spr_build_shops_small_01, 							"src/gfx/shops/shops_small_01/building.png") 					{template_house_80(1,1)}
spriteset (spr_build_shops_small_01_snow, 						"src/gfx/shops/shops_small_01/building.png") 					{template_house_80(1,82)}

// Ground Sprites
spriteset (spr_ground_shops_small_01, 							"src/gfx/shops/shops_small_01/ground.png") 						{template_house_80(1,1)}
spriteset (spr_ground_shops_small_01_snow, 						"src/gfx/shops/shops_small_01/ground.png") 						{template_house_80(1,82)}

// Normal Sprites
spritelayout sprlay_shops_small_01 {
	ground {
		sprite: spr_ground_shops_small_01(construction_state);
	}
	building {
		sprite: spr_build_shops_small_01(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_shops_small_01_snow {
	ground {
		sprite: spr_ground_shops_small_01_snow(construction_state);
	}
	building {
		sprite: spr_build_shops_small_01_snow(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_shops_small_01,terrain_type) {
	TILETYPE_SNOW: sprlay_shops_small_01_snow;
	sprlay_shops_small_01;
}

// Recolouring

random_switch (FEAT_HOUSES, SELF, rand_recolor_shops_small_01)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
	}


// Construction Check
switch (FEAT_HOUSES, SELF, switch_shops_small_01_con_check, 
	HasRoadAdjacentSW() &&		// Must have road to the SW
	TileGap(20)					// Can't appear within 20 tiles of another
) {return;}	

// Protection Check
switch (FEAT_HOUSES, SELF, switch_shops_small_01_pro_check, 
	FuncServicePASS() 	&&		// Must have PASS delivered
	(FuncServiceFOOD()	||		// Must have either FOOD delivered
	FuncServiceGOOD()	||		// or GOOD
	FuncServiceCASS()	||		// or CASS
	FuncServiceFRUT()   )		// or FRUT	
) {return;}	

item(FEAT_HOUSES, item_shops_small_01){
	property {
		substitute: 				29; 													
		name: 						string(NAME_SHOPS_SMALL_01);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED);		// Want flat land
		population: 				50;									// Agreed to JPS
		accepted_cargos: 			[
										[PASS, 4],[MAIL, 2],			// Of course
										[FOOD, 8],						// Food in general
										[GOOD, 8],						// Food in general
										[FRUT ,8],						// Fruit
										[LVST, 8],						// Meat
										[CASS, 8],						// Cassava
									];							
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1930,0xFFFF];	
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(
										TOWNZONE_CENTRE,
										TOWNZONE_INNER_SUBURB,
										TOWNZONE_OUTER_SUBURB
										//TOWNZONE_OUTSKIRT
										), 
									bitmask(
										CLIMATE_TEMPERATE, 
										CLIMATE_ARCTIC, 
										ABOVE_SNOWLINE
										)];
		//building_class:			2;	
		}
	graphics {
		default: 					switch_shops_small_01;
		construction_check:			switch_shops_small_01_con_check;
		protection:					switch_shops_small_01_pro_check;
		cargo_production: 			func_produce(10,3);
		colour: 					rand_recolor_shops_small_01;
	}
}

// End shops_small_01
