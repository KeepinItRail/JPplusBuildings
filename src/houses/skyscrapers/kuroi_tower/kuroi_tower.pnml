// Begin kuroi_tower

// Building Sprites
spriteset (spr_build_kuroi_tower, 				"src/houses/skyscrapers/kuroi_tower/building.png") 			{template_house_200(1,1)}
spriteset (spr_build_kuroi_tower_snow, 			"src/houses/skyscrapers/kuroi_tower/building.png") 			{template_house_200(1,202)}

// Ground Sprites
spriteset (spr_ground_kuroi_tower, 				"src/houses/skyscrapers/kuroi_tower/ground.png") 			{template_ground(1,1)}
spriteset (spr_ground_kuroi_tower_snow, 		"src/houses/skyscrapers/kuroi_tower/ground.png") 			{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_kuroi_tower {
	ground {
		sprite: spr_ground_kuroi_tower (3);
	}
	building {
		sprite: spr_build_kuroi_tower (3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_kuroi_tower_snow {
	ground {
		sprite: spr_ground_kuroi_tower_snow (3);
	}
	building {
		sprite: spr_build_kuroi_tower_snow (3);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_kuroi_tower,terrain_type) {
	TILETYPE_SNOW: sprlay_kuroi_tower_snow;
	sprlay_kuroi_tower;
}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_kuroi_tower_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	IsUniqueHouse() &&						// Only one in city
	NoProhibitionShrine() &&				// No Prohibition shrine to block construction
	SkyscraperRoads() &&					// If parameter enabled, must built on wide roads
	PopOver11L12LThreshold() &&				// Population of city check
	(!TileGapByClass(3) ||					// There is another skyscraper within n tiles
	IfFirstSkyscraper() )					// OR it's the first skyscraper
) {return;}	

item(FEAT_HOUSES, item_kuroi_tower){
	property {
		substitute: 				4; 													
		name: 						string(NAME_KUROI_TOWER);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED,HOUSE_FLAG_PROTECTED);
		population: 				220;
		accepted_cargos: 			[[PASS, 8],[MAIL, 8],[GOOD, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[2000,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				5;	
		}
	graphics {
		default: 					switch_kuroi_tower;
		construction_check:			switch_kuroi_tower_con_check;
		cargo_production: 			func_produce(16,6);
		colour: 					switch_standard_colors; 
	}
}

// End kuroi_tower
