// Begin modern_office_tower

// Building Sprites
spriteset (spr_build_modern_office_tower, 			"src/houses/skyscrapers/modern_office_tower/building.png") 				{template_house_200_wide(1,1)}
spriteset (spr_build_modern_office_tower_snow, 		"src/houses/skyscrapers/modern_office_tower/building.png") 				{template_house_200_wide(1,202)}

// Ground Sprites
spriteset (spr_ground_modern_office_tower, 			"src/houses/skyscrapers/modern_office_tower/ground.png") 				{template_ground(1,1)}
spriteset (spr_ground_modern_office_tower_snow, 	"src/houses/skyscrapers/modern_office_tower/ground.png") 				{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_modern_office_tower {
	ground {
		sprite: spr_ground_modern_office_tower(construction_state);
	}
	building {
		sprite: spr_build_modern_office_tower(construction_state);
		xoffset:  2;
		yoffset:  0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_modern_office_tower_snow {
	ground {
		sprite: spr_ground_modern_office_tower_snow(construction_state);
	}
	building {
		sprite: spr_build_modern_office_tower_snow(construction_state);
		xoffset:  2;
		yoffset:  0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_modern_office_tower,terrain_type) {
	TILETYPE_SNOW: sprlay_modern_office_tower_snow;
	sprlay_modern_office_tower;
}

// Recolouring

random_switch (FEAT_HOUSES, SELF, rand_recolor_modern_office_tower)
	{
		1: PALETTE_CC_RED;			
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;			
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}


// Construction Check
switch (FEAT_HOUSES, SELF, switch_modern_office_tower_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	IsUniqueHouse() &&						// Only one in city
	NoProhibitionShrine() &&				// No Prohibition shrine to block construction
	SkyscraperRoads() &&					// If parameter enabled, must built on wide roads
	PopOver9L10LThreshold() &&				// Population of city check
	(!TileGapByClass(3) ||					// There is another skyscraper within n tiles
	IfFirstSkyscraper() )					// OR it's the first skyscraper
) {return;}	

item(FEAT_HOUSES, item_modern_office_tower){
	property {
		substitute: 				4; 													
		name: 						string(NAME_MODERN_OFFICE_TOWER);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED,HOUSE_FLAG_PROTECTED);
		population: 				220;
		accepted_cargos: 			[[PASS, 8],[MAIL, 8],[GOOD, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[2000,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				5;	
		}
	graphics {
		default: 					switch_modern_office_tower;
		construction_check:			switch_modern_office_tower_con_check;
		cargo_production: 			func_produce(24,10);
		colour: 					rand_recolor_modern_office_tower; 
	}
}

// End modern_office_tower
