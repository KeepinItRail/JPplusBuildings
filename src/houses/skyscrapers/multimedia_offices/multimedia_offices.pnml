// Begin multimedia_offices

// Building Sprites
spriteset (spr_build_multimedia_offices, 			"src/houses/skyscrapers/multimedia_offices/building.png") 				{template_house_200_wide(1,1)}
spriteset (spr_build_multimedia_offices_snow, 		"src/houses/skyscrapers/multimedia_offices/building.png") 				{template_house_200_wide(1,202)}

// Ground Sprites
spriteset (spr_ground_multimedia_offices, 			"src/houses/skyscrapers/multimedia_offices/ground.png") 				{template_ground(1,1)}
spriteset (spr_ground_multimedia_offices_snow, 		"src/houses/skyscrapers/multimedia_offices/ground.png") 				{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_multimedia_offices {
	ground {
		sprite: spr_ground_multimedia_offices(construction_state);
	}
	building {
		sprite: spr_build_multimedia_offices(construction_state);
		xoffset: 3;
		yoffset: 0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_multimedia_offices_snow {
	ground {
		sprite: spr_ground_multimedia_offices_snow(construction_state);
	}
	building {
		sprite: spr_build_multimedia_offices_snow(construction_state);
		xoffset: 3;
		yoffset: 0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_multimedia_offices,terrain_type) {
	TILETYPE_SNOW: sprlay_multimedia_offices_snow;
	sprlay_multimedia_offices;
}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_multimedia_offices_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	IsUniqueHouse() &&						// Only one in city
	NoProhibitionShrine() &&				// No Prohibition shrine to block construction
	SkyscraperRoads() &&					// If parameter enabled, must built on wide roads
	PopOver11L12LThreshold() &&				// Population of city check
	(!TileGapByClass(3) ||					// There is another skyscraper within n tiles
	IfFirstSkyscraper() )					// OR it's the first skyscraper
) {return;}	

item(FEAT_HOUSES, item_multimedia_offices){
	property {
		substitute: 				4; 													
		name: 						string(NAME_MULTIMEDIA_OFFICES);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED,HOUSE_FLAG_PROTECTED);
		population: 				220;
		accepted_cargos: 			[[PASS, 8],[MAIL, 8],[GOOD, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1990,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				5;	
		}
	graphics {
		default: 					switch_multimedia_offices;
		construction_check:			switch_multimedia_offices_con_check;
		cargo_production: 			func_produce(24,10);
		//colour: 					rand_recolor_multimedia_offices; 
	}
}

// End multimedia_offices
