// Begin sato_building

// Building Sprites
spriteset (spr_build_sato_building, 			"src/houses/skyscrapers/sato_building/building.png") 			{template_house_200_wide(1,1)}
spriteset (spr_build_sato_building_snow, 		"src/houses/skyscrapers/sato_building/building.png") 			{template_house_200_wide(1,202)}

// Ground Sprites
spriteset (spr_ground_sato_building, 			"src/houses/skyscrapers/sato_building/ground.png") 				{template_ground(1,1)}
spriteset (spr_ground_sato_building_snow, 		"src/houses/skyscrapers/sato_building/ground.png") 				{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_sato_building {
	ground {
		sprite: spr_ground_sato_building (construction_state);
	}
	building {
		sprite: spr_build_sato_building (construction_state);
		//xoffset:  2;
		//yoffset:  0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_sato_building_snow {
	ground {
		sprite: spr_ground_sato_building_snow (construction_state);
	}
	building {
		sprite: spr_build_sato_building_snow (construction_state);
		//xoffset:  2;
		//yoffset:  0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_sato_building,terrain_type) {
	TILETYPE_SNOW: sprlay_sato_building_snow;
	sprlay_sato_building;
}

// Recolouring

random_switch (FEAT_HOUSES, SELF, rand_recolor_sato_building)
	{
		1: PALETTE_CC_RED;			
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;			
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}


// Construction Check
switch (FEAT_HOUSES, SELF, switch_sato_building_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	IsUniqueHouse() &&						// Only one in city
	NoProhibitionShrine() &&				// No Prohibition shrine to block construction
	SkyscraperRoads() &&					// If parameter enabled, must built on wide roads
	PopOver7L8LThreshold() &&				// Population of city check
	(!TileGapByClass(3) ||					// There is another skyscraper within n tiles
	IfFirstSkyscraper() )					// OR it's the first skyscraper
) {return;}	

item(FEAT_HOUSES, item_sato_building){
	property {
		substitute: 				4; 													
		name: 						string(NAME_SATO_BUILDING);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED,HOUSE_FLAG_PROTECTED);
		population: 				200;
		accepted_cargos: 			[[PASS, 8],[MAIL, 8],[GOOD, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1960,0xFFFF];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				5;	
		}
	graphics {
		default: 					switch_sato_building;
		construction_check:			switch_sato_building_con_check;
		cargo_production: 			func_produce(16,6);
		colour: 					rand_recolor_sato_building; 
	}
}

// End sato_building
