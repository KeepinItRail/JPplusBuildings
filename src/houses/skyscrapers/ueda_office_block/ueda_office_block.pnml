// Begin ueda_office_block

// Building Sprites
spriteset (spr_build_ueda_office_block, 			"src/houses/skyscrapers/ueda_office_block/building.png") 			{template_house_200_wide(1,1)}
spriteset (spr_build_ueda_office_block_snow, 		"src/houses/skyscrapers/ueda_office_block/building.png") 			{template_house_200_wide(1,202)}

// Ground Sprites
spriteset (spr_ground_ueda_office_block, 			"src/houses/skyscrapers/ueda_office_block/ground.png") 				{template_ground(1,1)}
spriteset (spr_ground_ueda_office_block_snow, 		"src/houses/skyscrapers/ueda_office_block/ground.png") 				{template_ground(1,33)}

// Normal Sprites
spritelayout sprlay_ueda_office_block {
	ground {
		sprite: spr_ground_ueda_office_block (construction_state);
	}
	building {
		sprite: spr_build_ueda_office_block (construction_state);
		//xoffset:  2;
		//yoffset:  0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_ueda_office_block_snow {
	ground {
		sprite: spr_ground_ueda_office_block_snow (construction_state);
	}
	building {
		sprite: spr_build_ueda_office_block_snow (construction_state);
		//xoffset:  2;
		//yoffset:  0;
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_ueda_office_block,terrain_type) {
	TILETYPE_SNOW: sprlay_ueda_office_block_snow;
	sprlay_ueda_office_block;
}

// Recolouring
/*
random_switch (FEAT_HOUSES, SELF, rand_recolor_ueda_office_block)
	{
		1: PALETTE_CC_RED;			
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;			
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}
*/

// Construction Check
switch (FEAT_HOUSES, SELF, switch_ueda_office_block_con_check, 
	FuncServicePASS() &&					// Must have passenger service
	IsCity() && 							// Can only appear in cities
	IsUniqueHouse() &&						// Only one in city
	PopOver9L10LCommercialThreshold() &&	// Population of city check
	Class5PopCheck() &&						// Residential population is sufficient for another Skyscraper
	ClusterBuildingClass() &&				// Must be within two tiles of the same building class
	IfFirstSkyscraper() 					// If first skyscraper, it must build beside a Class 2
) {return;}	

item(FEAT_HOUSES, item_ueda_office_block){
	property {
		substitute: 				4; 													
		name: 						string(NAME_UEDA_OFFICE_BLOCK);
		building_flags: 			bitmask(HOUSE_FLAG_NOT_SLOPED,HOUSE_FLAG_PROTECTED);
		population: 				200;
		accepted_cargos: 			[[PASS, 8],[MAIL, 8],[GOOD, 8]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				10;
		years_available: 			[1955,1989];
		minimum_lifetime: 			5;
		availability_mask: 			[bitmask(TOWNZONE_CENTRE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE)];
		building_class:				5;	
		}
	graphics {
		default: 					switch_ueda_office_block;
		construction_check:			switch_ueda_office_block_con_check;
		cargo_production: 			func_produce(16,6);
		//colour: 					rand_recolor_ueda_office_block; 
	}
}

// End ueda_office_block
