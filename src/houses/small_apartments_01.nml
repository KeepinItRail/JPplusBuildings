// Begin small_apartments_01

// Building Sprites
spriteset (spr_small_apartments_01, 							"src/gfx/small_apartments/small_apartments_01/building.png") 						{template_house_80(1,1)}
spriteset (spr_small_apartments_01_snow, 						"src/gfx/small_apartments/small_apartments_01/building.png") 						{template_house_80(1,82)}

// Ground Tiles
spriteset (spr_small_apartments_01_ground, 						"src/gfx/small_apartments/small_apartments_01/ground.png") 						{template_house_80(1,1)}
spriteset (spr_small_apartments_01_ground_snow, 				"src/gfx/small_apartments/small_apartments_01/ground.png") 						{template_house_80(1,82)}

// Normal Sprites
spritelayout sprlay_small_apartments_01_norm {
	ground {
		sprite: spr_small_apartments_01_ground(construction_state);
	}
	building {
		sprite: spr_small_apartments_01(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Snow Sprites
spritelayout sprlay_small_apartments_01_snow {
	ground {
		sprite: spr_small_apartments_01_ground_snow(construction_state);
	}
	building {
		sprite: spr_small_apartments_01_snow(construction_state);
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

// Choose between Normal Sprites and Snow Sprites
switch(FEAT_HOUSES, SELF, switch_small_apartments_01_snow,terrain_type) {
	TILETYPE_SNOW: sprlay_small_apartments_01_snow;
	sprlay_small_apartments_01_norm;
}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_small_apartments_01)
	{
		1: PALETTE_CC_RED;				
		2: PALETTE_CC_LIGHT_BLUE;
		//1: PALETTE_CC_PINK;			// Looks silly
		//1: PALETTE_CC_DARK_BLUE;		// Too in your face
		//1: PALETTE_CC_GREEN;			// Too in your face
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

// Construction Check
switch (FEAT_HOUSES, SELF, switch_small_apartments_01_con_check, HasRoadAdjacentSW() || HasRoadAdjacentNW()) {1: switch_small_flats_check_1; return 0;}

item(FEAT_HOUSES, item_small_apartments_01, 02, HOUSE_SIZE_1X1){
	property {
		substitute: 				02;
		name: 						TTD_STR_TOWN_BUILDING_NAME_HOUSES_1;
		population: 				30;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1945,0xFFFF];
		minimum_lifetime: 			4;
		availability_mask: 			[ALL_TOWNZONES & ~bitmask(TOWNZONE_CENTRE), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		
		}
	graphics {
		default: 					switch_small_apartments_01_snow;
		construction_check: 		switch_small_apartments_01_con_check;
		//cargo_production: 			func_produce(5,2,1);
		colour: 					rand_recolor_small_apartments_01;
	}
}

// End small_apartments_01
