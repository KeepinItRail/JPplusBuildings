// Begin townhouses_01

spriteset (spr_townhouses_01, 							"src/gfx/houses/townhouses_01/building.png") 						{template_house_1dir(1,1)}
spriteset (spr_townhouses_01_snow, 						"src/gfx/houses/townhouses_01/building.png") 						{template_house_1dir(1,80)}
spriteset (spr_townhouses_01_ground, 					"src/gfx/houses/townhouses_01/ground.png") 						{template_house_1dir(1,1)}
spriteset (spr_townhouses_01_ground_snow, 				"src/gfx/houses/townhouses_01/ground.png") 						{template_house_1dir(1,80)}

spritelayout sprlay_townhouses_01_norm {
	ground {
		sprite: spr_townhouses_01_ground(LOAD_TEMP(1));
	}
	building {
		sprite: spr_townhouses_01(LOAD_TEMP(1));
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

spritelayout sprlay_townhouses_01_snow {
	ground {
		sprite: spr_townhouses_01_ground_snow(LOAD_TEMP(1));
	}
	building {
		sprite: spr_townhouses_01_snow(LOAD_TEMP(1));
		recolour_mode: RECOLOUR_REMAP; 
		palette: PALETTE_USE_DEFAULT;
	}
}

switch (FEAT_HOUSES, SELF, switch_townhouses_01_sprites, [ 
 	// Check for snow and set the groundsprite in temporary variable 0
 	STORE_TEMP(GROUNDSPRITE_NORMAL, 0),
 	STORE_TEMP(terrain_type == TILETYPE_SNOW ? GROUNDSPRITE_SNOW : LOAD_TEMP(0), 0),
	// Check the rotation of the house
	STORE_TEMP(switch_road_adjacency(), 1),
	terrain_type == TILETYPE_SNOW])
	{1: sprlay_townhouses_01_snow; sprlay_townhouses_01_norm;}

// Recolouring
random_switch (FEAT_HOUSES, SELF, rand_recolor_townhouses_01)
	{
		1: PALETTE_CC_RED;
		2: PALETTE_CC_LIGHT_BLUE;
		1: PALETTE_CC_PINK;
		1: PALETTE_CC_DARK_BLUE;
		1: PALETTE_CC_GREEN;
		2: PALETTE_CC_DARK_GREEN;
		3: PALETTE_CC_CREAM;
		1: PALETTE_CC_MAUVE;
		4: PALETTE_CC_BROWN;
		4: PALETTE_CC_GREY;
		4: PALETTE_CC_WHITE;
	}

switch (FEAT_HOUSES, SELF, switch_townhouses_01_con_check, 
	HouseOfficeGap() 				// Can't build within two tiles of Building Class 2 (Offices, Shops and Skyscrapers)
) {return;}	

item(FEAT_HOUSES, item_townhouses_01, 06, HOUSE_SIZE_1X1){
	property {
		substitute: 				06;
		name: 						TTD_STR_TOWN_BUILDING_NAME_HOUSES_1;
		population: 				30;
		accepted_cargos: 			[[PASS, 4],[MAIL, 2]];
		local_authority_impact: 	80;
		removal_cost_multiplier: 	80;
		probability: 				1;
		years_available: 			[1945,0xFFFF];
		minimum_lifetime: 			2;
		availability_mask: 			[bitmask(TOWNZONE_INNER_SUBURB,TOWNZONE_CENTRE), ALL_CLIMATES | bitmask(ABOVE_SNOWLINE)];
		building_class:				1;
		
		}
	graphics {
		default: 					switch_townhouses_01_sprites;
		construction_check: 		switch_townhouses_01_con_check;
		cargo_production: 			func_produce(4,1);
		colour: 					rand_recolor_townhouses_01;
	}
}

// End townhouses_01
